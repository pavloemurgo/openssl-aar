buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.2'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "io.github.pavloemurgo.openssl"
    compileSdk 34

    defaultConfig {
        minSdk 21
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release")
    }
}

// Include prefab directory in the AAR for pre-built native libraries
android.libraryVariants.all { variant ->
    def variantName = variant.name.capitalize()
    tasks.named("bundle${variantName}Aar").configure {
        from("src/main/prefab") {
            into "prefab"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "com.github.pavloemurgo"
                artifactId = "openssl-aar"
                version = project.findProperty("VERSION") ?: "1.0.0"
            }
        }
    }
}
